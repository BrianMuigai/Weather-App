plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'com.google.android.libraries.mapsplatform.secrets-gradle-plugin' version '2.0.1' apply false
    // Add the App Distribution Gradle plugin
    id 'com.google.firebase.appdistribution'
    id 'com.google.gms.google-services'
}

/**
 * That gives you the possibility of 100 minor versions for every major version,
 * and 100 patch levels for every minor version.
 */
def versionMajor = 1
def versionMinor = 0
def versionPatch = 0
def internalTesting = ""

def standardAppName = "SampleWeatherApp"
def secretsPropertiesFile = rootProject.file("secrets.properties")
def secretProperties = new Properties()
secretProperties.load(new FileInputStream(secretsPropertiesFile))

android {
    namespace 'com.sampleweatherapp'
    compileSdk 33

    signingConfigs {
        release {
            storeFile rootProject.file('sampleWeatherApp.keystore')
            keyAlias secretProperties['keyAlias']
            keyPassword secretProperties['keyPassword']
            storePassword secretProperties['storePassword']
        }
        debug {
            storeFile rootProject.file('sampleWeatherAppDebug.keystore')
            keyAlias secretProperties['debugKeyAlias']
            keyPassword secretProperties['debugKeyPassword']
            storePassword secretProperties['debugStorePassword']
        }
    }

    defaultConfig {
        applicationId "com.sampleweatherapp"
        minSdk 24
        targetSdk 33
        versionCode versionMajor * 10000 +
                versionMinor * 100 +
                versionPatch
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        if (internalTesting) {
            versionName = "${versionName}-${internalTesting}"
        }

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }

        buildConfigField 'String', 'API_HOST', '"' + secretProperties['api_url'] + '"'
        def addConstant = {constantName, constantValue ->
            manifestPlaceholders += [ (constantName):constantValue]
            buildConfigField "String", "${constantName}", "\"${constantValue}\""
        }

        addConstant("MAPS_API_KEY", secretProperties['MAPS'])
        generateChangelog(rootProject.rootDir)
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            debuggable false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField 'String', 'OPEN_WEATHER_MAP_API_KEY', '"' + secretProperties['WEATHER_API_KEY'] + '"'
        }
        beta {
            initWith release
            applicationIdSuffix ".beta"
            debuggable true
            resValue "string", "app_name", "${standardAppName} beta"
            firebaseAppDistribution {
                artifactType="APK"
                releaseNotesFile="release.txt"
                groups="beta-team"
            }
        }

        debug {
            applicationIdSuffix ".debug"
            signingConfig signingConfigs.debug
            debuggable true
            resValue "string", "app_name", "${standardAppName} debug"
            buildConfigField 'String', 'OPEN_WEATHER_MAP_API_KEY', '"' + secretProperties['DEBUG_WEATHER_API_KEY'] + '"'
            firebaseAppDistribution {
                artifactType="APK"
                releaseNotesFile="release.txt"
                groups="internal-team"
            }
        }
    }
    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.3.2'
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
    // Customize your APK name here
    applicationVariants.all { variant ->
        def appName
        //Check if an applicationName property is supplied; if not use the name of the parent project.
        if (project.hasProperty("applicationName")) {
            appName = applicationName
        } else {
            appName = standardAppName
        }

        variant.outputs.all { output ->
            def versionName = variant.versionName
            def buildType = variant.buildType.name //e.g. debug
            ext.revision = getGitRevParseInfo("--short")
            ext.branch = getGitRevParseInfo("--abbrev-ref")

            //customize your app name by using variables
//            outputFileName = "${appName}-${buildType}-${versionName}-${ext.branch}-${ext.revision}.apk"
            outputFileName = "${appName}-${buildType}-${versionName}.apk"
        }
    }
}

static def getGitRevParseInfo(what) {
    def cmd = "git rev-parse " + what + " HEAD"
    def proc = cmd.execute()
    proc.text.trim()
}

static String generateChangelog(path) {
    println "Generating changelog.."
    //Get the last tag
    def lastTag = "git describe --tags --abbrev=0".execute().text.trim()
    //Get all the commits since the last tag
    def gitLogCmd = "git log $lastTag..HEAD --oneline --no-merges --pretty=format:\"%s\"".execute().text.trim()
    //Loop each line of the commits to build your changelog
    def changelog = "\""
    def file = new File(path, "release.txt")
    file.write("")
    gitLogCmd.eachLine { line ->
        //Remove surrounding quotation marks generated by the git log comand
        def escapedLine = line.substring(1, line.length() - 1)
        //Escape backslashes
        escapedLine = escapedLine.replaceAll(/(\\)/, "\\/")
        //Escape quotation marks
        escapedLine = escapedLine.replaceAll('"', '\\\\"')
        //Add each item to the changelog as a bullet point
        changelog += "• $escapedLine \\n"
        file.append("• $escapedLine \n")

    }

    //Close the changelog string
    changelog = (changelog + "\"").trim()
    //Useful log so you can see what was generated in the Gradle output
    println "Changelog generated, $changelog, from $lastTag to now."
    return changelog
}

dependencies {

    implementation 'androidx.navigation:navigation-runtime-ktx:2.5.2'
    implementation 'androidx.test:runner:1.4.0'
    def squareup_retrofit2_version = "2.9.0"
    def squareup_okhttp3_version = "4.10.0"

    implementation 'androidx.core:core-ktx:1.8.0'
    implementation platform('org.jetbrains.kotlin:kotlin-bom:1.8.0')
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.6.1'
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:2.6.1"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.6.1"
    implementation 'androidx.activity:activity-compose:1.5.1'
    implementation platform('androidx.compose:compose-bom:2022.10.00')
    implementation 'androidx.compose.ui:ui'
    implementation 'androidx.compose.ui:ui-graphics'
    implementation 'androidx.compose.ui:ui-tooling-preview'
    implementation 'androidx.compose.material3:material3'
    implementation 'androidx.navigation:navigation-compose:2.5.2'

    // Network
    implementation "com.squareup.retrofit2:retrofit:$squareup_retrofit2_version"
    implementation "com.squareup.retrofit2:converter-gson:$squareup_retrofit2_version"
    implementation "com.squareup.okhttp3:okhttp:$squareup_okhttp3_version"
    implementation "com.squareup.okhttp3:logging-interceptor:$squareup_okhttp3_version"

    //Location
    implementation 'com.google.android.gms:play-services-location:21.0.1'
    implementation 'com.google.maps.android:maps-compose:2.12.0'
    implementation 'com.google.android.gms:play-services-maps:18.1.0'
    implementation 'com.google.android.libraries.places:places:3.0.0'

    // Deployment
    implementation 'com.google.firebase:firebase-appdistribution-api:16.0.0-beta08'
    debugImplementation 'com.google.firebase:firebase-appdistribution:16.0.0-beta08'
    betaImplementation 'com.google.firebase:firebase-appdistribution:16.0.0-beta08'

    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.0.3'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation platform('androidx.compose:compose-bom:2022.10.00')
    androidTestImplementation 'androidx.compose.ui:ui-test-junit4'
    debugImplementation 'androidx.compose.ui:ui-tooling'
    debugImplementation 'androidx.compose.ui:ui-test-manifest'
}